#pragma kernel Density
#include "/Includes/Density.compute"
#include "/Includes/Noise.compute"

// Noise settings
StructuredBuffer<float3> offsets;
int octaves;
float lacunarity;
float persistence;
float noiseScale;
float noiseWeight;
float floorOffset;
float weightMultiplier;
bool closeEdges;
float hardFloor;
float hardFloorWeight;

float4 spawnCenter;
float spawnFalloff;

bool raiseEdges;
float edgeRaiseWidth;
float edgeRaiseHeight;

float4 params;

[numthreads(numThreads,numThreads,numThreads)]
void Density (int3 id : SV_DispatchThreadID)
{
    if (id.x >= numPointsPerAxis || id.y >= numPointsPerAxis || id.z >= numPointsPerAxis) {
        return;
    }

    float3 pos = centre + id * spacing - boundsSize/2;
    float offsetNoise = 0;

    float noise = 0;

    float frequency = noiseScale/100;
    float amplitude = 1;
    float weight = 1;
    for (int j =0; j < octaves; j ++) {
        float n = snoise((pos+offsetNoise) * frequency + offsets[j] + offset);
        float v = 1-abs(n);
        v = v*v;
        v *= weight;
        weight = max(min(v*weightMultiplier,1),0);
        noise += v * amplitude;
        amplitude *= persistence;
        frequency *= lacunarity;
    }
    
    float finalVal = -(pos.y + floorOffset) + noise * noiseWeight + (pos.y%params.x) * params.y;

    float distFromSpawn = distance(pos, spawnCenter.xyz);
    if (distFromSpawn < spawnCenter.w) {
        float fade = smoothstep(spawnCenter.w, spawnCenter.w - spawnFalloff, distFromSpawn);
        finalVal = lerp(-1.0, finalVal, fade);
    }

    if (pos.y < hardFloor) {
        finalVal += hardFloorWeight;
    }

    if (closeEdges) {
        float3 edgeOffset = abs(pos*2)-worldSize + spacing/2;
        float edgeWeight = saturate(sign(max(max(edgeOffset.x,edgeOffset.y),edgeOffset.z)));
        finalVal = finalVal * (1-edgeWeight) - 100 * edgeWeight;
    }

    if (raiseEdges) {
        float3 edgeOffset = abs(pos*2)-worldSize + spacing/2;
        float3 edgeFalloff = saturate(1.0 - edgeOffset / edgeRaiseWidth);
        float edgeStrength = max(max(edgeFalloff.x, edgeFalloff.z), 0);
        finalVal += edgeStrength * edgeRaiseHeight;
    }

    int index = indexFromCoord(id.x,id.y,id.z);
    points[index] = float4(pos, finalVal);
    
}
